# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# Maintainer: Thomas Labarussias <issif+aur@gadz.org>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributor: Kris NÃ³va R.I.P. <kris@nivenly.com>

pkgbase=falco-modules-dmks
pkgname=falco-modules-dmks
provides=(falco-modules-dmks)
pkgver=0.37.1
pkgrel=1
pkgdesc="Cloud native runtime security - dkms module"
arch=(x86_64)
license=(Apache)
depends=(falco dkms linux-headers)
url="https://github.com/falcosecurity/falco"
source_x86_64=(
    "https://download.falco.org/packages/bin/${arch}/falco-${pkgver}-x86_64.tar.gz"
    "falco-dkms.service"
)
sha256sums_x86_64=(
    '8d441495f72489be1bcab1ce8476ae26007fe2063c8053e8082b264066c46f25'
    'bc52a0b1d33407244de775d5a8d673ebb2b283aac177fcdebbd4ba1942275df2'
)
install="falco-dkms.install"

_driver="7.0.0+driver"

package() {
    install -d "${pkgdir}/usr/src/falco-${pkgver}"
    for _src in "${srcdir}/falco-${pkgver}-${arch}/usr/src/falco-${_driver}/"*; do
       [[ -f "$_src" ]] && install -Dm644 "$_src" "${pkgdir}/usr/src/falco-${pkgver}/$(basename "$_src")"
    done
    install -Dm644 "${srcdir}/falco-dkms.service" "${pkgdir}/usr/lib/systemd/system/falco-dkms.service"
}
