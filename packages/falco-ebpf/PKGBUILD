# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributor: Kris NÃ³va <kris@nivenly.com> R.I.P.

# WIP: This package should be renamed to falco-probe-ebpf
pkgbase=falco-ebpf
pkgname=("falco-ebpf")
pkgver=0.36.2
pkgrel=1
pkgdesc="Cloud native runtime security. eBPF (Perf Buffer)"
arch=(x86_64)
url="https://github.com/falcosecurity/falco"
license=("apache")
makedepends=(cmake git c-ares jq grpc yaml-cpp clang linux-headers llvm)
install=falco-ebpf.install
source=("falco-${pkgver}.tar.gz::https://github.com/falcosecurity/falco/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('b09786888fd6fa1e9f9958104a7a1b91282e95ace4f5b33d333704db76b2cf3c')

prepare() {
  cd "falco-${pkgver}"
  [[ -d build ]] || mkdir build
}

build() {
  cd "falco-${pkgver}/build"
  cmake .. \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_BUNDLED_DEPS=false \
    -DBUILD_BPF=ON

  make bpf
}

package() {
  cd "falco-${pkgver}/build"
  mkdir -p ${pkgdir}/usr/share/falco
  install -D -m0644 driver/bpf/probe.o ${pkgdir}/usr/share/falco/falco.o
}
