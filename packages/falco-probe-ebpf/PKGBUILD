# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributor: Kris NÃ³va <kris@nivenly.com> R.I.P.

pkgbase=falco-probe-ebpf
pkgname=falco-probe-ebpf
pkgdesc="Cloud native runtime security - eBPF probe"
pkgver=0.37.1
pkgrel=1
arch=(x86_64)
license=(Apache)
#depends=(falco)
makedepends=(cmake git c-ares jq grpc yaml-cpp clang linux-headers llvm)
url="https://github.com/falcosecurity/falco"
source_x86_64=(
    "falco-${pkgver}.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
    "falco-ebpf.service"
)
sha256sums_x86_64=(
    'f602bd025ff2997ecce1bd1f479592ab666276912d72212ab8d1fffd38ab8c94'
    'c4562a423d4b741c9d5a6d3e90c314f3a457617fd0812da7859d58b9cb4a8a54'
)
install="falco-ebpf.install"

prepare() {
    cd "${srcdir}/falco-${pkgver}"
    [[ -d build ]] || mkdir build
}

build() {
    cd "${srcdir}/falco-${pkgver}/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_BUNDLED_DEPS=false \
        -DBUILD_BPF=ON
    make bpf
}

package() {
    install -Dm644 "${srcdir}/falco-${pkgver}/build/driver/bpf/probe.o" "${pkgdir}/usr/share/falco/falco-bpf.o"
    install -Dm644 "${srcdir}/falco-ebpf.service" "${pkgdir}/usr/lib/systemd/system/falco-ebpf.service"
}
