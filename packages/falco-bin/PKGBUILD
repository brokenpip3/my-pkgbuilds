# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# Maintainer: Thomas Labarussias <issif+aur@gadz.org>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributor: Kris NÃ³va R.I.P. <kris@nivenly.com>

pkgbase=falco-bin
pkgname=falco-bin
provides=(falco)
conflicts=(falco)
backup=('etc/falco/falco_rules.yaml'  'etc/falco/falco.yaml')
pkgver=0.37.1
pkgrel=3
pkgdesc="Cloud native runtime security. Binary and config files"
arch=(x86_64)
license=(Apache)
optdepends=(
    "falco-probe-ebpf: ebpf probe"
    "falco-module-dkms: dkms module"
    "falcoctl: administrative tooling for Falco"
    "falcosidekick: connect Falco to your ecosystem"
)
url="https://github.com/falcosecurity/falco"
source_x86_64=(
    "https://download.falco.org/packages/bin/${arch}/falco-${pkgver}-x86_64.tar.gz"
    "falco.service"
)
sha256sums_x86_64=('8d441495f72489be1bcab1ce8476ae26007fe2063c8053e8082b264066c46f25'
                   '25c75419e786d6287555094746712f589f5e31ed569609604ceaf85ec2320be5')
install="falco.install"

package() {
    for cf in falco-${pkgver}-${arch}/etc/falco/*.yaml; do
        install -Dm644 ${cf} "${pkgdir}/etc/falco/$(basename $cf)"
    done
    install -d "${pkgdir}/etc/falco/rules.d"
    sed -i 's#probe: ${HOME}/.falco/falco-bpf.o#probe: /usr/share/falco/falco-bpf.o#' "${pkgdir}/etc/falco/falco.yaml"
    install -Dm755 falco-${pkgver}-${arch}/usr/bin/falco "${pkgdir}/usr/bin/falco"
    install -Dm644 "${srcdir}/falco.service" "${pkgdir}/usr/lib/systemd/system/falco.service"
}
